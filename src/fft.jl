# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

@kwdef struct FFTGP{V,N,D} <: GeoStatsProcess
  variogram::V = GaussianVariogram()
  mean::Float64 = 0.0
  minneighbors::Int = 1
  maxneighbors::Int = 10
  neighborhood::N = nothing
  distance::D = Euclidean()
end

function randprep(::AbstractRNG, process::FFTGP, setup::RandSetup)
  # retrive variogram model and mean
  Î³ = process.variogram
  Î¼ = process.mean

  # check stationarity
  if !isstationary(Î³)
    throw(ArgumentError("variogram model must be stationary"))
  end

  dom = setup.domain
  data = setup.geotable
  grid, _ = unview(dom)
  dims = size(grid)
  center = CartesianIndex(dims .Ã· 2)
  cindex = LinearIndices(dims)[center]

  # number of threads in FFTW
  FFTW.set_num_threads(setup.threads)

  # perform Kriging in case of conditional simulation
  pred = nothing
  if !isnothing(data)
    krig = Kriging(Î³, Î¼)
    (; minneighbors, maxneighbors, neighborhood, distance) = process
    pred = fitpredict(krig, data, dom; minneighbors, maxneighbors, neighborhood, distance)
  end

  pairs = map(setup.vartypes, setup.varnames) do V, var
    # compute covariances between centroid and all points
    ð’Ÿc = [centroid(grid, cindex)]
    ð’Ÿp = [centroid(grid, i) for i in 1:nelements(grid)]
    cs = sill(Î³) .- Variography.pairwise(Î³, ð’Ÿc, ð’Ÿp)
    C = reshape(cs, dims)

    # move to frequency domain
    F = sqrt.(abs.(fft(fftshift(C))))
    F[1] = zero(V) # set reference level

    # get variable prediction and data locations if necessary
    zÌ„, dinds = nothing, nothing
    if !isnothing(pred)
      zÌ„ = pred[:, var]
      # find data locations in target domain
      ddom = domain(data)
      searcher = KNearestSearch(dom, 1)
      found = [search(centroid(ddom, i), searcher) for i in 1:nelements(ddom)]
      dinds = unique(first.(found))
    end

    # save preprocessed inputs for variable
    var => (; F, zÌ„, dinds)
  end

  Dict(pairs)
end

function randsingle(rng::AbstractRNG, process::FFTGP, setup::RandSetup, prep)
  # retrieve domain info
  dom = setup.domain
  data = setup.geotable
  grid, inds = unview(dom)
  dims = size(grid)

  # retrive variogram model and mean
  Î³ = process.variogram
  Î¼ = process.mean

  varreal = map(setup.vartypes, setup.varnames) do V, var
    # unpack preprocessed parameters
    (; F, zÌ„, dinds) = prep[var]

    # perturbation in frequency domain
    P = F .* exp.(im .* angle.(fft(rand(rng, V, dims))))

    # move back to time domain
    Z = real(ifft(P))

    # adjust mean and variance
    ÏƒÂ² = Statistics.var(Z, mean=zero(V))
    Z .= âˆš(sill(Î³) / ÏƒÂ²) .* Z .+ Î¼

    # unconditional realization
    záµ¤ = Z[inds]

    # perform conditioning if necessary
    z = if isnothing(data)
      záµ¤ # we are all set
    else
      # view realization at data locations
      ktab = (; var => view(záµ¤, dinds))
      kdom = view(dom, dinds)
      kdata = georef(ktab, kdom)

      # perform Kriging prediction
      krig = Kriging(Î³, Î¼)
      (; minneighbors, maxneighbors, neighborhood, distance) = process
      pred = fitpredict(krig, kdata, kdom; minneighbors, maxneighbors, neighborhood, distance)
      zÌ„áµ¤ = pred[:, var]

      # add residual field
      zÌ„ .+ (záµ¤ .- zÌ„áµ¤)
    end

    var => z
  end

  Dict(varreal)
end
