# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    LindgrenProcess(range=1.0, sill=1.0; init=NearestInit())

Lindgren process with given `range` (correlation length)
and `sill` (total variance) as described in Lindgren 2011.
Optionally, specify the data initialization method `init`.

The process relies relies on a discretization of the Laplace-Beltrami 
operator on meshes and is adequate for highly curved domains (e.g. surfaces).

## References

* Lindgren et al. 2011. [An explicit link between Gaussian fields and
  Gaussian Markov random fields: the stochastic partial differential
  equation approach](https://rss.onlinelibrary.wiley.com/doi/10.1111/j.1467-9868.2011.00777.x)
"""
struct LindgrenProcess{â„’<:Len,V,I} <: FieldProcess
  range::â„’
  sill::V
  init::I
  LindgrenProcess(range::â„’, sill::V, init::I) where {â„’<:Len,V,I} = new{float(â„’),float(V),I}(range, sill, init)
end

LindgrenProcess(range=1.0u"m", sill=1.0; init=NearestInit()) = LindgrenProcess(addunit(range, u"m"), sill, init)

function randprep(::AbstractRNG, process::LindgrenProcess, ::DefaultRandMethod, setup::RandSetup)
  # retrieve setup paramaters
  (; domain, geotable, varnames, vartypes) = setup

  # retrieve sill and range
  ð“ = process.range
  Ïƒ = process.sill

  # retrieve initialization method
  init = process.init

  # sanity checks
  @assert domain isa Mesh "domain must be a `Mesh`"
  @assert ð“ > zero(ð“) "range must be positive"
  @assert Ïƒ > zero(Ïƒ) "sill must be positive"

  # Laplace-Beltrami operator
  W = laplacematrix(domain)
  M = measurematrix(domain)
  Î” = inv(M) * W

  # retrieve parametric dimension
  d = paramdim(domain)

  # LHS of SPDE (ÎºÂ² - Î”)Z = Ï„W with Î” = Mâ»Â¹W
  Î± = 2
  Î½ = Î± - d / 2
  Îº = 1 / ð“
  A = Îº^2 * I - Î”

  # MatÃ©rn precision matrix
  Ï„Â² = Ïƒ^2 * Îº^(2Î½) * (4Ï€)^(d / 2) * gamma(Î±) / gamma(Î½)
  Q = ustrip.(A'A / Ï„Â²)

  # factorization
  F = cholesky(Array(Q))
  L = inv(Array(F.U))

  # initialize buffers for realizations and simulation mask
  pset = PointSet(vertices(domain))
  vars = Dict(zip(varnames, vartypes))
  buff, mask = initbuff(pset, vars, init, data=geotable)

  # result of preprocessing
  pairs = map(varnames) do var
    # retrieve buffer and mask for variable
    z = buff[var]
    m = mask[var]

    # retrieve data locations and data values
    iâ‚ = findall(m)
    zâ‚ = view(z, iâ‚)

    # retrieve simulation locations
    iâ‚‚ = setdiff(1:nvertices(domain), iâ‚)

    # interpolate at simulation locations if necessary
    zÌ„ = if isempty(iâ‚)
      nothing
    else
      z[iâ‚‚] .= -Q[iâ‚‚,iâ‚‚] \ (Q[iâ‚‚,iâ‚] * zâ‚)
      z
    end

    # save preprocessed inputs for variable
    var => (; Q, L, iâ‚, iâ‚‚, zÌ„)
  end

  Dict(pairs)
end

function randsingle(rng::AbstractRNG, ::LindgrenProcess, ::DefaultRandMethod, setup::RandSetup, prep)
  # retrieve setup paramaters
  (; domain, geotable, varnames, vartypes) = setup

  # simulation at vertices
  varreal = map(varnames, vartypes) do var, V
    # unpack preprocessed parameters
    (; Q, L, iâ‚, iâ‚‚, zÌ„) = prep[var]

    # unconditional realization
    w = randn(rng, V, nvertices(domain))
    záµ¤ = L * w

    # perform conditioning if necessary
    z = if isempty(iâ‚)
      záµ¤ # we are all set
    else
      # view realization at data locations
      záµ¤â‚ = view(záµ¤, iâ‚)

      # interpolate at simulation locations
      zÌ„áµ¤ = similar(záµ¤)
      záµ¤â‚‚ = -Q[iâ‚‚,iâ‚‚] \ (Q[iâ‚‚,iâ‚] * záµ¤â‚)
      zÌ„áµ¤[iâ‚] .= záµ¤â‚
      zÌ„áµ¤[iâ‚‚] .= záµ¤â‚‚

      # add residual field
      zÌ„ .+ (záµ¤ .- zÌ„áµ¤)
    end

    var => z
  end

  # vertex table
  vtable = (; varreal...)

  # change of support
  vdata = GeoTable(domain; vtable)
  edata = integrate(vdata, varnames...)

  # columns of element table
  cols = Tables.columns(values(edata))
  Dict(var => Tables.getcolumn(cols, var) for var in varnames)
end
